^0 ศูนย์
1 หนึ่ง
2 สอง
3 สาม
4 สี่
5 ห้า
6 หก
7 เจ็ด
8 แปด
9 เก้า

# 2 digits edge cases
11 สิบเอ็ด
21 ยี่สิบเอ็ด
(\d)1 $1สิบเอ็ด # unit digit being '1'
1(\d) สิบ$1 
2(\d) ยี่สิบ$1

(\d)(\d) $1สิบ$2

(\d)01 $1ร้อยเอ็ด # for edge case of unit digit being '1'
(\d)(\d\d) $1ร้อย$2

(\d)001 $1พันเอ็ด # for edge case of unit digit being '1'
(\d)(\d{3}) $1พัน$2

(\d)0001 $1หมื่นเอ็ด # for edge case of unit digit being '1'
(\d)(\d{4}) $1หมื่น$2

(\d)00001 $1แสนเอ็ด # for edge case of unit digit being '1'
(\d)(\d{5}) $1แสน$2

# million and beyond (Arbitrarily looped since no official guidelines for trillion and beyond)
(\d{1,20})000001 $1ล้านเอ็ด # for edge case of unit digit being '1'
(\d{1,20})(\d{6}) $1ล้าน$2


# negative numbers

[-−](\d+) ลบ|$1

# decimals

([-−]?\d+)[.,] |$1|จุด
([-−]?\d+[.,]\d*)(\d) |$1|$2

# currency

"THB ([-−]?\d+)" |$1|บาทถ้วน

# currency with decimals

"(THB [-−]?\d+)[,.]00" $1
"THB ([-−]?\d+)[,.](\d{1,2})" |$1|บาท$2สตางค์
 
# other currencies?

[A-Z]{3}
"([A-Z]{3}) ([-−]?\d[\d.,]*)" $2$1

== ordinal ==

([-−]?\d+([.,]\d+)?) ที่$1

== ordinal-number ==

(\d+)	\1.

== help ==

"" |$(1)|, |$(2)|, |$(3)|\n$(help ordinal)$(help ordinal-number)
(.*) \1: |$(\1 1)|, |$(\1 2)|, |$(\1 3)|\n
